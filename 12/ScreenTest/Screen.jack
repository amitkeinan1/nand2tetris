// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 256 rows (indexed 0..255, top to bottom)
 * of 512 pixels each (indexed 0..511, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
    static boolean currColor;

    /** Initializes the Screen. */
    function void init() {
        let currColor = true;
//        do Screen.clearScreen();
        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int address;
        let address = 16384;
        while (address <  24576){
            do Memory.poke(address, 0);
            let address = address + 1;
        }
        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let currColor = b;
        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int address;
        var int value;
        var int bitCurrValue;
        var int bitDecimalValue;
        var int xModSixteen;
        let address = (32*y) + (x/16);
        let value = Memory.peek(16384 + address);
        let xModSixteen = x-(16*(x/16));
        // change bit x%16 bit to currColor
        let bitCurrValue = Math.getBit(value, xModSixteen);

        if  ((bitCurrValue = 1) & ~currColor) // pixel is black and needs to be white
        {
            let value = value - Math.powerOfTwo(xModSixteen);
        }
        else
        {
            if((bitCurrValue = 0) & currColor){ // pixel is white and needs to be black
                let value = value + Math.powerOfTwo(xModSixteen);
            }
        }
        do Memory.poke(16384 + address, value);
        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int xBegin; // TODO: vars are useless here
        var int yBegin;
        var int xEnd;
        var int yEnd;
        var int dx;
        var int dy;
        let yBegin = Math.min(y1, y2);
        let yEnd = Math.max(y1, y2);
        let dx = x2 - x1;

        if (dx = 0){
            do Screen.drawVerticalLine(yBegin, yEnd, x1);
            return;
        }
        let xBegin = Math.min(x1, x2);
        let xEnd = Math.max(x1, x2);
        let dy = y2- y1;
        if (dy = 0){
            do Screen.drawHorizontalLine(xBegin, xEnd, y1);
            return;
        }
        if ((dx*dy) > 0){
            if (dx >0){
                do Screen.drawLineIncrease(x1, y1, x2, y2);
            }
            else{
                do Screen.drawLineIncrease(x2, y2, x1, y1);
            }
            return;
        }
        if (dx > 0){
                do Screen.drawLineYDecrease(x1, y1, x2, y2);
            }
            else{
                do Screen.drawLineYDecrease(x2, y2, x1, y1);
            }
        return;
    }

    /** TODO */
    function void drawLineIncrease(int x1, int y1, int x2, int y2){
        var int dx;
        var int dy;
        var int currX;
        var int currY;
        var int diff;
        let dx = x2 - x1;
        let dy = y2 - y1;
        let currX = x1;
        let currY = y1;
        let diff = 0;
        while (~(currX = (x2+1)) | ~(currY = (y2+1))){
            do Screen.drawPixel(currX, currY);
            if (diff < 0){
                let currX = currX + 1;
                let diff = diff + dy;
            }
            else{
                let currY = currY + 1;
                let diff = diff - dx;
            }
        }
        return;
    }

    /** TODO */
    function void drawLineYDecrease(int x1, int y1, int x2, int y2){
        var int dx;
        var int dy;
        var int currX;
        var int currY;
        var int diff;
        let dx = x2 - x1;
        let dy = y1 - y2;
        let currX = x1;
        let currY = y1;
        let diff = 0;
        while (~(currX = (x2+1)) | ~(currY = (y2-1))){
            do Screen.drawPixel(currX, currY);
            if (diff < 0){
                let currX = currX + 1;
                let diff = diff + dy;
            }
            else{
                let currY = currY - 1;
                let diff = diff - dx;
            }
        }
        return;
    }

    /** TODO */
    function void drawHorizontalLine(int xBegin, int xEnd, int y){
        var int currX;
        let currX = xBegin;
        while (currX < (xEnd+1)){
            do Screen.drawPixel(currX, y);
            let currX = currX + 1;
        }
        return;
    }

    /** TODO */
    function void drawVerticalLine(int yBegin, int yEnd, int x){
        var int currY;
        let currY = yBegin;
        while (currY < (yEnd+1)){
            do Screen.drawPixel(x, currY);
            let currY = currY + 1;
        }
        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int currY;
        var int currX;
        if (x1 > x2) {
            do Output.printString("x1 must be smaller than x2"); // TODO: remove before submission
            return;
        }
        if (y1 > y2) {
            do Output.printString("y1 must be smaller than y2"); // TODO: remove before submission
            return;
        }

        /* The following if statement is used to save expensive function calls. We prefer to draw less lines which are
        longer, instead of drawing a lot of short lines which is less efficient. */
        if ((x2 - x1) >(y2 - y1)){ // if the rectangle is wide and short
            let currY = y1;
            while (currY < (y2 + 1)){
                do Screen.drawHorizontalLine(x1, x2, currY);
                let currY = currY + 1;
            }
        }
        else{  // if the rectangle is tall and narrow
            let currX = x1;
            while (currX < (x2 + 1)){
                do Screen.drawVerticalLine(y1, y2, currX);
                let currX = currX + 1;
            }
        }
        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int dy;
        var int xDiff;
        var int currY;
        if (r > 181){ // 181 is the limit we have to set in order to avoid overflow on the hack computer hardware
            do Output.printString("r is too big"); // TODO: remove before submission
            return;
        }
        let dy = -r;
        while (~(dy > r)){
            let xDiff = Math.sqrt((r*r) - (dy*dy));
            let currY = y + dy;
            do Screen.drawLine(x - xDiff, currY, x + xDiff, currY);
            let dy = dy + 1;
        }
        return;
    }
}
